{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d9f1583e-9fec-4f1e-a83b-17683fc17529",
   "metadata": {},
   "outputs": [],
   "source": [
    "#import Pkg; Pkg.add(\"JuMP\")\n",
    "#import Pkg; Pkg.add(\"HiGHS\")\n",
    "#import Pkg; Pkg.add(\"XLSX\")\n",
    "#import Pkg; Pkg.add(\"DataFrames\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "d934ecbd-0031-42aa-b5dc-f02ed7cf05fd",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Used libraries\n",
    "using JuMP, HiGHS, XLSX"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "1dac8bb4-c1e8-4831-bbef-3720a0c2d35f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# File to read\n",
    "FileName = \"Set_Or35_De70_Inst_1.xlsx\" ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "48756321-5e8b-495e-a39a-75f626e97ea4",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Basic data loading\n",
    " J = XLSX.readdata(FileName, \"Parameters\", \"C1\")  # Products (items)\n",
    " T = XLSX.readdata(FileName, \"Parameters\", \"C2\")  # Planning horizon\n",
    " M = XLSX.readdata(FileName, \"Parameters\", \"C3\")  # Machines\n",
    " K = XLSX.readdata(FileName, \"Parameters\", \"C4\")  # Orders\n",
    "\n",
    "# Data reading\n",
    "p  = Array{Float64, 2}(undef, J, 2)   # productivities\n",
    "cp = Array{Float64, 2}(undef, J, 2)  # production costs\n",
    "st = Array{Float64, 2}(undef, J, 2)  # setup times\n",
    "\n",
    "PRO = XLSX.readdata(FileName, \"Production\", \"A2:A$(J+1)\")  # product\n",
    "OPS = XLSX.readdata(FileName, \"Production\", \"B2:B$(J+1)\")  # manufacturing options\n",
    "MA1 = XLSX.readdata(FileName, \"Production\", \"C2:C$(J+1)\")  # first choice for manufacturing\n",
    "p[:,1]  = XLSX.readdata(FileName, \"Production\", \"D2:D$(J+1)\")  # productivity\n",
    "cp[:,1] = XLSX.readdata(FileName, \"Production\", \"E2:E$(J+1)\")  # production cost\n",
    "st[:,1] = XLSX.readdata(FileName, \"Production\", \"F2:F$(J+1)\")  # setup time\n",
    "MA2 = XLSX.readdata(FileName, \"Production\", \"G2:G$(J+1)\")  # second choice for manufacturing \n",
    "p[:,2]  = XLSX.readdata(FileName, \"Production\", \"H2:H$(J+1)\")  # productivity\n",
    "cp[:,2] = XLSX.readdata(FileName, \"Production\", \"I2:I$(J+1)\")  # production cost\n",
    "st[:,2] = XLSX.readdata(FileName, \"Production\", \"J2:J$(J+1)\")  # setup time\n",
    "ch = XLSX.readdata(FileName, \"Production\", \"K2:K$(J+1)\")   # inventory cost\n",
    "s  = XLSX.readdata(FileName, \"Production\", \"L2:L$(J+1)\")   # initial stock \n",
    "\n",
    "MAQ = XLSX.readdata(FileName, \"Machine\", \"A2:A$(M+1)\")  # machine\n",
    "n   = XLSX.readdata(FileName, \"Machine\", \"B2:B$(M+1)\")  # daily productive time\n",
    "g   = XLSX.readdata(FileName, \"Machine\", \"C2:C$(M+1)\")  # maximum daily overtime\n",
    "co  = XLSX.readdata(FileName, \"Machine\", \"D2:D$(M+1)\")  # overtime cost\n",
    "ce  = XLSX.readdata(FileName, \"Machine\", \"E2:E$(M+1)\")  # extra shift cost\n",
    "r  = XLSX.readdata(FileName, \"Machine\", \"F2:F$(M+1)\")   # maximum daily hours per extra shift\n",
    "\n",
    "a   = XLSX.readdata(FileName, \"Day\", \"B2:B$(T+1)\")   # day, availability to work\n",
    "u   = XLSX.readdata(FileName, \"Day\", \"C2:C$(T+1)\") ; # crew availability\n",
    "\n",
    "# Data reading (orders)\n",
    "xf = XLSX.readxlsx(FileName) \n",
    "sh = xf[\"Demand\"]\n",
    "ORD    = sh[\"A2:A$(K+1)\"]    # order number\n",
    "ORD_CL = sh[\"B2:B$(K+1)\"]    # order customer\n",
    "d      = sh[\"C2:C$(K+1)\"]    # due date order\n",
    "f      = sh[\"D2:D$(K+1)\"]    # number of periods allowed to cover backorder\n",
    "q      = sh[\"E2:V$(K+1)\"]    # quantity of product j in order i, q[k,j]\n",
    "cb     = sh[\"W2:AN$(K+1)\"] ; # backorder cost (customer), cb[k,j]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "8b3d3ac3-b122-4d33-9f98-fde82987c4ff",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Data generation\n",
    "aux = zeros(Int8, (M, J))   # indica en po(i,j) 0 no es opcion 1 primera 2 segunda\n",
    "for j = 1:J\n",
    "    for i = 1:M\n",
    "        if cmp(MAQ[i],MA1[j])==0 \n",
    "            aux[i,j]=1\n",
    "        end\n",
    "         if cmp(MAQ[i],MA2[j])==0\n",
    "            aux[i,j]=2\n",
    "        end\n",
    "    end\n",
    "end\n",
    " \n",
    "G=1+16*max( maximum(p) ) ;  # big-M parameter"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "a693eba1-70a1-4958-a810-faccc2b5f33b",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Lot sizing model\n",
    "LSModel = Model(HiGHS.Optimizer) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "3b990a1d-dc56-42cd-a05d-f8751a9206aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Model variables\n",
    "@variable(LSModel, X[1:J, 1:2, 1:T] >= 0) # quantity of product j manufactured in the choice-machine [1,2] on day t\n",
    "@variable(LSModel, Y[1:J, 1:T, 1:K] >= 0) # quantity of product j, manufactured in period t, used to partially cover order k\n",
    "@variable(LSModel, E[1:M, 1:T] >= 0)      # overtime used on machine i on day t\n",
    "@variable(LSModel, Z[1:M, 1:T], Bin)      # indicates with value=1, if on day t an extra shift is used on machine I\n",
    "@variable(LSModel, U[1:J, 1:2, 1:T], Bin) # indicates with value=1, if on day t product j is manufactured on machine I\n",
    "@variable(LSModel, S[1:J, 1:K] >= 0)     # quantity of product j from the initial stock used to partially cover order k\n",
    "@variable(LSModel, PCost >= 0)     # ...\n",
    "@variable(LSModel, ICost >= 0)     # ...\n",
    "@variable(LSModel, BCost >= 0)     # ...\n",
    "@variable(LSModel, ECost >= 0)     # ...\n",
    "@variable(LSModel, ZCost >= 0)   ;  # ..."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "ebb89e95-8e0e-4d6a-82a8-a61aac6e58af",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Objective Function\n",
    "@constraint(LSModel, P_cost, sum( cp[j,1]*X[j,1,t]  for j in 1:J, t in 1:T ) \n",
    "        + sum( cp[j,2]*X[j,2,t]  for j in 1:J, t in 1:T  if OPS[j]==2 )  == PCost) \n",
    "\n",
    "@constraint(LSModel, I_cost, sum(Y[j,t,k]*ch[j]*(d[k]-t)  for j in 1:J, k in 1:K, t in 1:d[k]-1 ) \n",
    "        + sum(S[j,k]*ch[j]*(d[k]-1)  for j in 1:J, k in 1:K ) == ICost )\n",
    "\n",
    "@constraint(LSModel, B_cost, sum(Y[j,t,k]*cb[k,j]*(t-d[k]) for j in 1:J, k in 1:K, t in d[k]+1:min(d[k]+f[k],T) ) == BCost )\n",
    "\n",
    "@constraint(LSModel, E_cost, sum(E[i,t]*co[i]  for i in 1:M, t in 1:T ) == ECost )\n",
    "\n",
    "@constraint(LSModel, Z_cost, sum(Z[i,t]*ce[i]  for i in 1:M, t in 1:T ) == ZCost  )\n",
    "\n",
    "@objective(LSModel, Min, PCost + ICost + BCost + ECost + ZCost ) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "465bb19a-0131-4fa4-9f36-7f4fb14be364",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "11c71e7f-9f40-4b0e-8390-7bc2db11a543",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Constraints"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "cb9b80a8-b27c-487e-a2e1-da039b0836e9",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Restricts the manufacturing of products that only have one manufacturing option\n",
    "@constraint(LSModel,prod_null, \n",
    "    sum(X[j,2,t] for j in 1:J, t in 1:T if OPS[j]==1) == 0 ) \n",
    "\n",
    "@constraint(LSModel,bin_prod_null, \n",
    "    sum(U[j,2,t] for j in 1:J, t in 1:T if OPS[j]==1) == 0 ) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "fea86e9b-a76e-4303-a3ea-2207b89356d9",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Production capacity (hours)\n",
    "@expression(LSModel, setup[i = 1:M, t = 1:T],\n",
    "           sum( st[j,aux[i,j]]*U[j,aux[i,j],t] for j in 1:J if aux[i,j]!=0) ) \n",
    "\n",
    "@expression(LSModel, capacity[i = 1:M, t = 1:T],\n",
    "         a[t]*(n[i] + E[i,t] + Z[i,t]*r[i]) - setup[i,t] ) \n",
    "\n",
    "@constraint(LSModel, capacity_used[i = 1:M, t = 1:T], \n",
    "         sum(X[j,1,t]/p[j,1] for j =1:J if cmp(MAQ[i],MA1[j])==0) + sum(X[j,2,t]/p[j,2] for j =1:J if cmp(MAQ[i],MA2[j])==0)  <=  capacity[i,t]) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "a7d1ec01-d488-4d81-bc2d-20e803f7cf08",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Max. daily productive overtime (hours)\n",
    "@constraint(LSModel, overtime_used[i = 1:M, t = 1:T],\n",
    "        E[i,t] <= (1-Z[i,t])*g[i]   ) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "862b20e8-973c-41e5-91a4-af8e0da7b59d",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Max. daily hours per extra shift \n",
    "@constraint(LSModel, extra_shift_used[t = 1:T],\n",
    "        sum(Z[i,t] for i in 1:M ) <= a[t]*u[t]   )  ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "867415c3-9e3f-48ec-9b0e-f6a9cc52443d",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Consumption of the initial stock\n",
    "@constraint(LSModel, initial_stock_used[j = 1:J],\n",
    "        sum(S[j,k] for k in 1:K)   == s[j]  ) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "7671fb32-3112-4948-8599-f899abff95dc",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Production to order\n",
    "@constraint(LSModel, production_used[j = 1:J, t = 1:T],\n",
    "        X[j,1,t] + X[j,2,t]*(OPS[j]-1) == sum(Y[j,t,k] for k in 1:K if t<=d[k]+f[k])   ) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "21b34c8c-0a32-47dd-ad43-aaa5e9a76f5d",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Order fulfillment\n",
    "@constraint(LSModel, order_fulfillment[j = 1:J, k = 1:K],\n",
    "        S[j,k] + sum(Y[j,t,k] for t in 1:min(d[k]+f[k],T) ) == q[k,j] ) ;    "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "bc54e383-7511-469b-9890-5cc104f066ff",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Binary production for setups\n",
    "@constraint(LSModel,bin_prod_ub[j = 1:J, t = 1:T, o = 1:2], \n",
    "    U[j,o,t] <= G*X[j,o,t] )\n",
    "\n",
    "@constraint(LSModel,bin_prod_lb[j = 1:J, t = 1:T, o = 1:2], \n",
    "    G*U[j,o,t] >= X[j,o,t] ) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "f9fadc2e-cc66-458e-9630-abcc01920ef9",
   "metadata": {},
   "outputs": [],
   "source": [
    "set_silent(LSModel)\n",
    "optimize!(LSModel) ;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "7c35c17d-7bb4-4a30-bc4d-8232a372bce0",
   "metadata": {},
   "outputs": [],
   "source": [
    "# OutputFile, print reports\n",
    "\n",
    "OutName = string(\"Report_\",FileName) \n",
    "if termination_status(LSModel) == OPTIMAL\n",
    "    XLSX.openxlsx(OutName, mode=\"w\") do xf\n",
    "        sheet = xf[1]\n",
    "        XLSX.rename!(sheet, \"products_R\")\n",
    "        sheet[1,1] = \"Day\"\n",
    "        sheet[1,2] = \"Product\"\n",
    "        sheet[1,3] = \"Machine first option\"\n",
    "        sheet[1,4] = \"Quantity(1)\"\n",
    "        sheet[1,5] = \"Machine second option\"\n",
    "        sheet[1,6] = \"Quantity(2)\"\n",
    "        row=2\n",
    "        for t = 1:T\n",
    "            for j = 1:J\n",
    "                sheet[row,1] = t\n",
    "                sheet[row,2] = PRO[j]\n",
    "                sheet[row,3] = MA1[j]\n",
    "                sheet[row,4] = value(X[j,1,t])\n",
    "                if OPS[j] == 2\n",
    "                    sheet[row,5] = MA2[j]\n",
    "                    sheet[row,6] = value(X[j,2,t])\n",
    "                end\n",
    "                row += 1 \n",
    "            end  \n",
    "        end        \n",
    "           \n",
    "        XLSX.addsheet!(xf, \"machines_R\")\n",
    "        xf[2][1,1] = \"Day\"\n",
    "        xf[2][1,2] = \"Machine\"\n",
    "        xf[2][1,3] = \"Overtime used\"\n",
    "        xf[2][1,4] = \"Extra shift used\"\n",
    "        xf[2][1,5] = \"Slack\"\n",
    "        xf[2][1,6] = \"Production (item, quantity ...)\"\n",
    "        row=2\n",
    "        for t = 1:T\n",
    "            for i = 1:M\n",
    "                xf[2][row,1] = t\n",
    "                xf[2][row,2] = MAQ[i]\n",
    "                xf[2][row,3] = value(E[i,t])\n",
    "                if value(Z[i,t]) > 0.5\n",
    "                    xf[2][row,4] = \"YES\"\n",
    "                    else xf[2][row,4] = \"no\"\n",
    "                end\n",
    "                xf[2][row,5] = round(normalized_rhs(capacity_used[i,t]) - value(capacity_used[i,t]), digits = 6)\n",
    "                col=6\n",
    "                for j = 1:J\n",
    "                    if aux[i,j] != 0 && value(X[j,aux[i,j],t]) != 0 \n",
    "                        xf[2][row,col] = PRO[j]\n",
    "                        xf[2][row,col+1] = value(X[j,aux[i,j],t])\n",
    "                        col = col + 2\n",
    "                    end\n",
    "                end\n",
    "                row += 1 \n",
    "            end  \n",
    "        end        \n",
    "    \n",
    "        XLSX.addsheet!(xf, \"order_fulfillment_R\")\n",
    "        xf[3][1,1] = \"Order\"\n",
    "        xf[3][1,2] = \"Customer\"\n",
    "        xf[3][1,3] = \"Due date\"\n",
    "        xf[3][1,4] = \"Product\"\n",
    "        xf[3][1,5] = \"Ordered_quantity\"\n",
    "        xf[3][1,6] = \"Fulfillment (day/quantity): (day 0, represent initial stock)\"\n",
    "        row = 2\n",
    "        for k = 1:K\n",
    "            for j = 1:J\n",
    "                xf[3][row,1] = ORD[k]\n",
    "                xf[3][row,2] = ORD_CL[k]\n",
    "                xf[3][row,3] = d[k]\n",
    "                xf[3][row,4] = PRO[j]\n",
    "                xf[3][row,5] = q[k,j]\n",
    "                col = 6\n",
    "                if q[k,j] != 0 \n",
    "                    if value(S[j,k]) != 0\n",
    "                        xf[3][row,col] = 0\n",
    "                        xf[3][row,col+1] = value(S[j,k])\n",
    "                        col = col + 2\n",
    "                    end\n",
    "                    for t = 1:min(d[k]+f[k],T)\n",
    "                        if value(Y[j,t,k]) != 0\n",
    "                            xf[3][row,col] = t\n",
    "                            xf[3][row,col+1] = value(Y[j,t,k])\n",
    "                            col = col + 2\n",
    "                        end\n",
    "                    end\n",
    "                end\n",
    "                row += 1\n",
    "            end\n",
    "        end\n",
    "    \n",
    "        XLSX.addsheet!(xf, \"costs_R\")\n",
    "        xf[4][1,1] =  \"Production cost\"\n",
    "        xf[4][2,1] =  \"Inventory cost\"\n",
    "        xf[4][3,1] =  \"Backorder cost\"\n",
    "        xf[4][4,1] =  \"Overtime cost\"\n",
    "        xf[4][5,1] =  \"Extra-shift cost\"\n",
    "        xf[4][6,1] =  \"Total cost\"\n",
    "        xf[4][1,2] =  value.(PCost)\n",
    "        xf[4][2,2] =  value.(ICost)\n",
    "        xf[4][3,2] =  value.(BCost)\n",
    "        xf[4][4,2] =  value.(ECost)\n",
    "        xf[4][5,2] =  value.(ZCost)\n",
    "        xf[4][6,2] =  objective_value(LSModel)\n",
    "    \n",
    "        # Control sheet\n",
    "        XLSX.addsheet!(xf, \"control_R\")\n",
    "        for j = 1:J\n",
    "            xf[5][1+j,1] =  \"Backorder (ton)\"\n",
    "            xf[5][1+j,2] =  PRO[j]\n",
    "        end\n",
    "        for t = 1:T\n",
    "            sref = string(\"day \",t)\n",
    "            xf[5][1,t+2] =  sref\n",
    "        end\n",
    "        back = zeros(Float16, (J,T)) \n",
    "        for j = 1:J, t = 1:T\n",
    "            for k = 1:K\n",
    "                if t == d[k]\n",
    "                    aux_value = 0.0\n",
    "                    init = 1 + d[k]\n",
    "                    fini = min(d[k]+f[k],T)\n",
    "                    if init <= fini\n",
    "                        aux_value = sum(value(Y[j,tt,k]) for tt in init:fini )\n",
    "                    end\n",
    "                    back[j,t] += aux_value\n",
    "                end\n",
    "            end\n",
    "            xf[5][1+j,t+2] = back[j,t]\n",
    "        end\n",
    "    \n",
    "        for i = 1:M\n",
    "            xf[5][1+J+i,1] =  \"Slack (hrs)\"\n",
    "            xf[5][1+J+i,2] =  MAQ[i]\n",
    "        end\n",
    "        for i = 1:M, t = 1:T\n",
    "            xf[5][1+J+i,t+2] = round(normalized_rhs(capacity_used[i,t]) - value(capacity_used[i,t]), digits = 6)\n",
    "        end\n",
    "    \n",
    "        XLSX.addsheet!(xf, \"Sol_summ_R\")\n",
    "        xf[6][1,1] = string(solution_summary(LSModel))\n",
    "        xf[6][3,1] = \"termination_status\"\n",
    "        xf[6][3,2] = string(termination_status(LSModel))\n",
    "        xf[6][4,1] = \"objective_value\"\n",
    "        xf[6][4,2] = objective_value(LSModel)\n",
    "        xf[6][5,1] = \"result_count\"\n",
    "        xf[6][5,2] = result_count(LSModel)\n",
    "        xf[6][6,1] = \"solve_time (sec.)\"\n",
    "        xf[6][6,2] = solve_time(LSModel)\n",
    "\n",
    "        \n",
    "\n",
    "        XLSX.addsheet!(xf, \"Stock_R\")    \n",
    "        for j = 1:J\n",
    "            xf[7][j,1] = PRO[j]\n",
    "            stock_level = 0\n",
    "            for t = 1:T\n",
    "                if t==1  stock_level = s[j]        + value(X[j,1,1]) + value(X[j,2,1])\n",
    "                else     stock_level = stock_level + value(X[j,1,t]) + value(X[j,2,t])\n",
    "                end\n",
    "                for k = 1:K\n",
    "                    if d[k]==t   stock_level = stock_level - q[k,j]\n",
    "                    end\n",
    "                end\n",
    "                xf[7][j,t+1] = stock_level\n",
    "            end\n",
    "        end\n",
    "\n",
    "                \n",
    "    end # end do\n",
    "    \n",
    "else print(FileName,\" Not solved\")\n",
    "end ;\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bd5ca00c-be1a-4023-9404-5e70cdb6427e",
   "metadata": {},
   "outputs": [],
   "source": [
    "set_attribute(LSModel, \"time_limit\", 60.0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e34a0385-67f8-4837-9885-c2703902e9b4",
   "metadata": {},
   "outputs": [],
   "source": [
    "optimize!(LSModel) "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4a5086d2-5f06-47eb-ab22-15348b0cb22e",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f4d9eebc-acb0-48b1-a02e-d03cfb51c52f",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0dbd8ca4-4965-4b1f-b63b-7d9f19aa901f",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.2",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
